plugins {
    id 'java'
    id 'jacoco'
    id 'org.springframework.boot' version '3.5.9'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.asciidoctor.jvm.convert' version '4.0.5'
    id 'com.diffplug.spotless' version '6.25.0'
    id 'com.epages.restdocs-api-spec' version '0.19.4'
}

group = 'com.sipomeokjo'
version = '0.0.1-SNAPSHOT'
description = ''

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
//	implementation 'org.springframework.boot:spring-boot-starter-batch'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework:spring-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-mysql'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'net.ttddyy:datasource-proxy:1.10'
    implementation platform('software.amazon.awssdk:bom:2.30.27')
    implementation 'software.amazon.awssdk:s3'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    runtimeOnly 'com.mysql:mysql-connector-j'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.batch:spring-batch-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.19.4'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

tasks.named('asciidoctor') {
    inputs.dir snippetsDir
    dependsOn test
}

spotless {
    java {
        target 'src/*/java/**/*.java'
        googleJavaFormat().aosp()

        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

openapi3 {
    server = 'http://localhost:8080'
    title = 'API'
    description = 'API description'
    version = '0.1.0'
    format = 'yaml'
}

tasks.register("copyOasToSwagger", Copy) {
    dependsOn("openapi3")

    delete("src/main/resources/static/swagger/openapi3.yaml")
    from(layout.buildDirectory.file("api-spec/openapi3.yaml"))
    into("src/main/resources/static/swagger/")
}

tasks.named("bootJar") { dependsOn("copyOasToSwagger") }
